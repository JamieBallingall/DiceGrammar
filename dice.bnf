<natural> ::= [1-9] [0-9]*
<whole> ::= "0" | <natural>
<integer> ::= "-" <natural> | <natural>

<w> ::= " "*

<wholelist> ::= <whole> | <whole> <w> "," <w> <wholelist>
<distn> ::= "[" <w> <integer> <w> ";" <w> <wholelist> <w> "]"
<dexpr> ::= "d" <natural> | <natural> "d" <natural> | "d%" | <natural> "d%" | "dF" | <natural> "dF"
<const> ::= <integer>
<distnlist> ::= <multiset> | <multiset> <w> "," <w> <distnlist>

<multiset> ::= <distn> 
             | <const>
             | <dexpr> 
             | "{" <w> <distnlist> <w> "}"
             | <multiset> <w> "+" <w> <multiset>
             | <multiset> <w> "-" <w> <multiset>
             | <multiset> <function>

<le> ::= "<="

<ge> ::= ">="

<gorl> ::= <le> | <ge>

<explode> ::= "X" | "!"

<times> ::= "x" | "*"

<highlow> ::= "H" | "L"

<function> ::= "sum"
			       | <highlow>
		      	 | "K" <highlow> <natural>
             | "D" <highlow> <natural>
             | <explode> <times> <natural>
             | <explode> <times> <natural> <gorl> <integer>
             | "R" <gorl> <integer>
             | "R" <times> <natural> <gorl> <integer>
